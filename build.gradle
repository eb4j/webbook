//
// webook gradle build script.
//
// Copyright (c) 2016 Hiroshi Miura
//
//

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.asciidoctor:asciidoctorj-diagram:1.3.1'
        classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.11'
    }
}

plugins {
    // github
    id "co.riiid.gradle" version "0.4.2"
    // bintray
    id "com.jfrog.bintray" version "1.6"
    // Publish to gh-pages
    id 'org.ajoberstar.github-pages' version '1.1.0'
    // AsciiDoc
    id 'org.asciidoctor.gradle.asciidoctor' version '1.5.1'
    id 'org.asciidoctor.convert' version '1.5.3'
}

// Release management
version = projectVersion

apply from: "$rootDir/gradle/github.gradle"
apply from: "$rootDir/gradle/bintray.gradle"
apply from: "$rootDir/gradle/ghpages.gradle"

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'war'

sourceCompatibility = jdkVersion
targetCompatibility = jdkVersion
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
[compileJava, compileTestJava]*.options*.bootClasspath = "$JDK_HOME/jre/lib/rt.jar"

// Quality managements
apply plugin: 'jacoco'
apply from: "$rootDir/gradle/integration-test.gradle"
apply from: "$rootDir/gradle/checkstyle.gradle"
apply from: "$rootDir/gradle/findbugs.gradle"

repositories {
    jcenter()
}

dependencies {
    compile files("$rootDir/libs/eb4j-core-1.99.0.jar"),
        'commons-io:commons-io:1.4',
        'commons-lang:commons-lang:2.4'

    providedCompile 'javax.servlet:servlet-api:2.5',
        'javax.servlet.jsp:jsp-api:2.1',
        'javax.servlet:jstl:1.2',
        'org.springframework:spring-webmvc:2.5.6.SEC01',
        'commons-logging:commons-logging:1.1.1',
        'org.slf4j:slf4j-api:1.5.10',
        'org.slf4j:slf4j-log4j12:1.5.10'

    testCompile 'org.testng:testng:6.9.10',
                'org.springframework:spring-test:2.5.6.SEC01'
}

test {
    useTestNG()
}


if (gradle.startParameter.taskNames.contains('bintrayUpload')  ||
    gradle.startParameter.taskNames.contains('githubRelease')) {
    apply from: "$rootDir/gradle/build.publish.gradle"
}
